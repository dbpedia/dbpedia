<?xml version="1.0" encoding="utf-8"?>
<mx:Box 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:components="com.hillelcoren.components.*"
	width="100%" height="100%"
	verticalAlign="top" horizontalAlign="left" 
	paddingLeft="20" paddingTop="15" paddingBottom="20" paddingRight="20"
	initialize="init()">
	
	<mx:Script>
		<![CDATA[
			import mx.utils.StringUtil;
			import mx.core.ByteArrayAsset;
			import com.hillelcoren.utils.StringUtils;
			import mx.collections.ArrayCollection;
			
			[Bindable]
			private var _contacts:ArrayCollection;
			
			[Bindable]  
			[Embed(source="simpsons.txt", mimeType="application/octet-stream")]  
			private var myFileClass:Class; 
			
			private function init():void
			{
				_contacts = new ArrayCollection();
				
				var MyFileByteArray:ByteArrayAsset = ByteArrayAsset(new myFileClass());  
				var simpsons:String = MyFileByteArray.readUTFBytes(MyFileByteArray.length);
				
				for each (var simpson:String in simpsons.split( "\r" ))
				{
					var parts:Array = simpson.split( "\t" );
					
					var obj:Object = new Object();
					obj.name = StringUtil.trim( parts[0] );
					obj.email = String( obj.name.split( " " )[0] ).toLowerCase() + "@gmail.com";
					_contacts.addItem( obj ); 
				}				
			}
			
			public function dropDownLabelFunction( item:Object ):String
			{
				var label:String = item.name + " &lt;" + item.email + "&gt;";
				
				var str:String = "";
				var searchStr:String = autocomplete.searchText;
				
				var start:int = label.search(new RegExp(searchStr, "gi"));
				
				str = label.substring(0, start);
				str = str + "<b><u>" + label.substr(start, searchStr.length) + "</u></b>";
				str = str + label.substr(start + searchStr.length, label.length);

				if (autocomplete.isItemSelected( item ))
    			{
    				str = "<font color='" + Consts.COLOR_TEXT_DISABLED + "'>" + str + "</font>";
    			}
    			
    			return str;
			}

			private function filterFunction( item:Object, searchStr:String ):Boolean
			{
				if (StringUtils.anyWordBeginsWith( item.name, searchStr ))
				{
					return true;
				}
				else if (StringUtils.beginsWith( item.email, searchStr ))
				{	
					return true;
				}
				
				return false;
			}
			
		]]>
	</mx:Script>
	

	<mx:Form width="100%" height="100%">
		<mx:FormItem label="To: ">				
			<components:AutoComplete id="autocomplete" dataProvider="{ _contacts }" width="350"
				isMultiSelect="true" multiSelectLayout="horizontal" isStrict="false" areNewItemsEditable="true"
				labelField="name" dropDownLabelFunction="dropDownLabelFunction" filterFunction="filterFunction"/>
		</mx:FormItem>
		<mx:FormItem label="Subject: ">
			<mx:TextInput width="350"/>
		</mx:FormItem>
		<mx:FormItem label="Body:  " height="100%">
			<mx:TextArea width="350" height="100%"/>
		</mx:FormItem>
	</mx:Form>
	
</mx:Box>